/*
 * Copyright (C) 2015-2016 Daniel Schaal <daniel@schaal.email>
 *
 * This file is part of OCReader.
 *
 * OCReader is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * OCReader is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with OCReader.  If not, see <http://www.gnu.org/licenses/>.
 *
 */

apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 28

    dataBinding.enabled = true

    defaultConfig {
        applicationId "email.schaal.ocreader"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 47
        versionName "0.44"

        archivesBaseName="${getApplicationId()}_${getVersionCode()}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

ext {
    versions = [
            okhttp: '4.0.1',
            retrofit: '2.6.1',
            glide: '4.9.0',
            acra: '5.2.0'
    ]
}

dependencies {
    implementation "androidx.core:core:1.0.2"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.palette:palette:1.0.0"

    // Fix for glide still using com.android.support namespace
    implementation 'com.android.support:support-annotations:28.0.0'
    annotationProcessor 'com.android.support:support-annotations:28.0.0'

    implementation('com.mikepenz:materialdrawer:6.1.1@aar') {
        transitive = true
    }

    implementation('com.mikepenz:aboutlibraries:6.2.0@aar') {
        transitive = true
    }

    implementation 'io.realm:android-adapters:3.0.0'

    implementation 'org.jsoup:jsoup:1.12.1'

    implementation "com.squareup.okhttp3:okhttp:${versions.okhttp}"

    implementation "com.squareup.retrofit2:retrofit:${versions.retrofit}"
    implementation "com.squareup.retrofit2:converter-moshi:${versions.retrofit}"

    implementation("com.github.bumptech.glide:glide:${versions.glide}@aar") {
        transitive = true
    }
    implementation "com.github.bumptech.glide:okhttp3-integration:${versions.glide}@aar"
    implementation "com.github.bumptech.glide:recyclerview-integration:${versions.glide}@aar"

    annotationProcessor "com.github.bumptech.glide:compiler:${versions.glide}"

    implementation 'com.github.zafarkhaja:java-semver:0.9.0'

    releaseImplementation "ch.acra:acra-mail:${versions.acra}"
    releaseImplementation "ch.acra:acra-dialog:${versions.acra}"

    implementation('com.vdurmont:emoji-java:4.0.0', {
        exclude group: 'org.json', module: 'json'
    })

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'

    androidTestImplementation "com.squareup.okhttp3:mockwebserver:${versions.okhttp}"

    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation "org.robolectric:robolectric:4.1"
    testImplementation 'junit:junit:4.12'
}
